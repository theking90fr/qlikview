///$tab Main
SET ThousandSep=' ';
SET DecimalSep=',';
SET MoneyThousandSep=' ';
SET MoneyDecimalSep=',';
SET MoneyFormat='# ##0,00 €;-# ##0,00 €';
SET TimeFormat='hh:mm:ss';
SET DateFormat='DD/MM/YYYY';
SET TimestampFormat='DD/MM/YYYY hh:mm:ss[.fff]';
SET MonthNames='janv.;févr.;mars;avr.;mai;juin;juil.;août;sept.;oct.;nov.;déc.';
SET DayNames='lun.;mar.;mer.;jeu.;ven.;sam.;dim.';


SET vDataSourceFolder=..\..\DataStore\Extract\V12\Prod\;
SET vDataDestinationFolder=..\..\DataStore\Transform\;
SET vDataTempFolder=..\..\DataStore\Temp\;

temp_devises_brutes:
LOAD 
	CUARAT, 
	CUCONO, 
	CUCRTP, 
	CUCUCD, 
	num(date#(CUCUTD,'YYYYMMDD')) as CUCUTD, 
	CUDIVI, 
	CUDMCU, 
	CULOCD, 
	CURAFA
FROM
[$(vDataSourceFolder)CCURRA.QVD]
(qvd);

LET _start_date = num(makedate(1999));
LET _end_date = num(today());

temp_calendrier_originel:
LOAD
	$(_start_date) + IterNo() - 1 as temp_date
AUTOGENERATE 1 WHILE $(_start_date) + IterNo() - 1 <= $(_end_date);


temp_couple_devises:
LOAD distinct
	CULOCD,
	CUCUCD
RESIDENT temp_devises_brutes;


FOR ligneCouple = 0 to NoOfRows('temp_couple_devises')-1
	LET vDeviseSource = peek('CUCUCD', $(ligneCouple), 'temp_couple_devises'); 
	LET vDeviseCible = peek('CULOCD', $(ligneCouple), 'temp_couple_devises'); 

	TRACE $(vDeviseSource) -> $(vDeviseCible);

	temp_calendrier_courant:
	LOAD
		temp_date as date_taux,
		'$(vDeviseSource)' as devise_source,
		'$(vDeviseCible)' as devise_cible
	RESIDENT temp_calendrier_originel;

	left join (temp_calendrier_courant)
	LOAD
		CUCUTD as date_taux,
		CUARAT as taux
	RESIDENT temp_devises_brutes
	WHERE CULOCD='$(vDeviseSource)' and CUCUCD='$(vDeviseCible)';

	STORE temp_calendrier_courant INTO [$(vDataTempFolder)temp_work.qvd];

	DROP TABLE temp_calendrier_courant;

	temp_change:
	LOAD @1 as date_taux, 
	     @2 as devise_source, 
	     @3 as devise_cible, 
	     @4 as taux
	FROM
	[$(vDataTempFolder)temp_work.qvd]
	(qvd, filters(
	Replace(4, top, StrCnd(null))
	));

	STORE temp_change into [$(vDataDestinationFolder)taux_$(vDeviseSource)_vers_$(vDeviseCible).qvd];
	DROP TABLE temp_change;
NEXT;

DROP TABLES temp_devises_brutes, temp_calendrier_originel, temp_couple_devises;
