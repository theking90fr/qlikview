///$tab Main
SET ThousandSep=' ';
SET DecimalSep=',';
SET MoneyThousandSep=' ';
SET MoneyDecimalSep=',';
SET MoneyFormat='# ##0,00 €;-# ##0,00 €';
SET TimeFormat='hh:mm:ss';
SET DateFormat='DD/MM/YYYY';
SET TimestampFormat='DD/MM/YYYY hh:mm:ss[.fff]';
SET MonthNames='janv.;févr.;mars;avr.;mai;juin;juil.;août;sept.;oct.;nov.;déc.';
SET DayNames='lun.;mar.;mer.;jeu.;ven.;sam.;dim.';


//Connexion M3 PRD

ODBC CONNECT32 TO [M3 PROD] (XUserId is MGIGVRdNHTbeWZBZ, XPassword is UOALJIJKCaUKDbELaKNIFED);


SET vRefTablesChamps = ..\..\DataStore\Config\REF_EXTRACT_M3_PRD.xlsx;
SET vQVDFolder = ..\..\DataStore\Extract\M3\Prod\;
SET vQVDFolderSplit = $(vQVDFolder)Split\;


Tables_A_Extraire:
LOAD BIBLIOTHEQUE,
	 TABLE, 
     EXPORT,
     HISTORIQUE_MAX, 
     HISTORIQUE_MIN, 
     SPLIT, 
     CONDITION
FROM
[$(vRefTablesChamps)]
(ooxml, embedded labels, table is TABLES);

FOR i = 0 to (NoOfRows('Tables_A_Extraire')-1)

	LET vBibliotheque = peek('BIBLIOTHEQUE', $(i), 'Tables_A_Extraire');
	LET vTable = peek('TABLE', $(i), 'Tables_A_Extraire');
	LET vExport = peek('EXPORT', $(i), 'Tables_A_Extraire');
	LET vSplit = peek('SPLIT', $(i), 'Tables_A_Extraire');
	LET vCondition = peek('CONDITION', $(i), 'Tables_A_Extraire');	
	LET vHistoriqueMin = peek('HISTORIQUE_MIN', $(i), 'Tables_A_Extraire');	
	LET vHistoriqueMax = peek('HISTORIQUE_MAX', $(i), 'Tables_A_Extraire');	


	IF ('$(vExport)'='1' or '$(vExport)'='O' or '$(vExport)'='o') THEN
	//Si la table est à exporter
			TRACE Export de la table $(vTable) ligne $(i);

			IF (isnull('$(vSplit)') or trim('$(vSplit)')='') THEN
			// Si on ne splitte pas la table
				TRACE Export complet de la table $(vTable);

				//Préparation de la clause SELECT
				Champs_A_Extraire:
				LOAD distinct concat(CHAMP, ', ') as CHAMPS
				FROM
				[$(vRefTablesChamps)]
				(ooxml, embedded labels, table is CHAMPS)
				WHERE TABLE = '$(vTable)';
				
				LET vClauseSelect = peek('CHAMPS', -1, 'Champs_A_Extraire');
				IF ('$(vClauseSelect)'='') THEN
					SET vClauseSelect=*;
				ENDIF;
				DROP TABLE Champs_A_Extraire;
			
				//Préparation de la clause WHERE
				
				IF (not (isnull('$(vCondition)') or trim('$(vCondition)')='')) THEN
				//Si la condition est renseignée
					LET vClauseFrom = 'WHERE '&'$(vCondition)';
				ELSE
					LET vClauseFrom = '';
				ENDIF;
				
				
				$(vTable):
				SQL SELECT $(vClauseSelect)
				FROM $(vBibliotheque).$(vTable)
				$(vClauseFrom);

				STORE $(vTable) into [$(vQVDFolder)$(vTable).QVD] (qvd);
				
				DROP TABLE $(vTable);
						
			ELSE
			// Si on splitte la table

					TRACE Export partiel de la table $(vTable);
					
					//Récupération des dates min et max
					MinMax_TMP:
					SQL SELECT 
						min($(vSplit)) AS MINMOIS, 
						max($(vSplit)) AS MAXMOIS
					FROM
					$(vBibliotheque).$(vTable);
					
					
					LET _moisdeb = peek('MINMOIS', -1, 'MinMax_TMP');
					IF $(vHistoriqueMin) > 0 THEN
						LET _moisdeb = date(rangemax(date#(peek('MINMOIS', 0, 'MinMax_TMP'), 'YYYYMM'), monthstart(addmonths(today(), -$(vHistoriqueMax)))));
					ELSE 
						LET _moisdeb = date(date#(peek('MINMOIS', 0, 'MinMax_TMP'), 'YYYYMM'));
					ENDIF
					LET _moisfin = date(date#(peek('MAXMOIS', 0, 'MinMax_TMP'), 'YYYYMM'));
					
					DROP TABLE MinMax_TMP;
				
					//Préparation de la clause SELECT
					Champs_A_Extraire:
					LOAD distinct concat(CHAMP, ', ') as CHAMPS
					FROM
					[$(vRefTablesChamps)]
					(ooxml, embedded labels, table is CHAMPS)
					WHERE TABLE = '$(vTable)';
					
					LET vClauseSelect = peek('CHAMPS', -1, 'Champs_A_Extraire');
					IF ('$(vClauseSelect)'='') THEN
						SET vClauseSelect=*;
					ENDIF;
					DROP TABLE Champs_A_Extraire;
				
					//Préparation de la clause WHERE
					
					IF (not (isnull('$(vCondition)') or trim('$(vCondition)')='')) THEN
					//Si la condition est renseignée
						LET vClauseFrom = ' and ('&'$(vCondition)'&')';
					ELSE
						LET vClauseFrom = '';
					ENDIF;
					
				
					LET _i = 0;
		
					// On boucle sur tous les mois.
					DO WHILE addmonths(_moisdeb, _i) <= _moisfin
						//	response = MsgBox(_i);
					
						LET _aaaa = year(addmonths('$(_moisdeb)', $(_i)));
						LET _mm = right('0' & num(month(addmonths('$(_moisdeb)', $(_i)))), 2);
					
						IF (FileSize('$(vQVDFolderSplit)$(vTable)__$(_aaaa)$(_mm).QVD')>0 and addmonths(_moisdeb, _i) <= addmonths(_moisfin, -vHistoriqueMin)) THEN
							TRACE Le mois $(_mm)/$(_aaaa) existe déjà;
						ELSE
							TRACE Création du mois $(_mm)/$(_aaaa);

							$(vTable):
							SELECT $(vClauseSelect)
							FROM $(vBibliotheque).$(vTable)
							WHERE $(vSplit) = $(_aaaa)$(_mm) $(vClauseFrom);
							
							STORE $(vTable) into [$(vQVDFolderSplit)$(vTable)__$(_aaaa)$(_mm).QVD] (qvd);
							
							// On supprime la table qui est chargée en mémoire.
							DROP TABLE $(vTable);

						ENDIF;
						
				
						
						LET _i = _i + 1;
					LOOP
						
			ENDIF;
	
	ENDIF;
	

NEXT i;
